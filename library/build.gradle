apply plugin: 'com.android.library'

android {
    compileSdkVersion sdkVersion
    buildToolsVersion '24.0.0'

    defaultConfig {
        versionCode 7
        versionName libVersion

        minSdkVersion 8
        targetSdkVersion sdkVersion

        consumerProguardFiles 'proguard-library.txt'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    compile project(':commons')
    testCompile 'junit:junit:4.12'
    androidTestCompile project(':testing')
    androidTestCompile 'com.android.support.test:runner:0.5'
}

apply from: "$rootProject.projectDir/maven-aar.gradle"

// android lint.jar configuration

configurations {
    lintChecks
}

dependencies {
    lintChecks project(path: ':linter', configuration: 'lintChecks')
}

task copyLinterJar(type: Copy) {
    from(configurations.lintChecks) {
        rename { 'lint.jar' }
    }
    into "$project.buildDir/intermediates/lint/"
}

project.afterEvaluate {
    def compileLintTask = project.tasks.find { it.name == 'compileLint' }
    compileLintTask.dependsOn(copyLinterJar)
}
